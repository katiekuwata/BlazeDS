<?xml version="1.0" encoding="utf-8"?>
<mx:List xmlns:mx="http://www.adobe.com/2006/mxml"
	dropShadowEnabled="true"
	currentState="hidden"
	labelField="name"
	keyDown="searchKeyDownHandler(event)"
	click="openSelectedItem()">

	<mx:Metadata>
		[Event(name="select")]
	</mx:Metadata>	
	
	<mx:Script>
		<![CDATA[
			import mx.rpc.events.ResultEvent;

			import mx.collections.ArrayCollection;

			public function search(searchStr:String):void
			{
				dataProvider = null;
				srv.getProductsByName(searchStr);
				if (currentState == "hidden") currentState = "";
			}
			
			protected function searchKeyDownHandler(event:KeyboardEvent):void
			{
				switch (event.keyCode) 
				{
					case Keyboard.ENTER:
						openSelectedItem();
	    				break;
					case Keyboard.ESCAPE:
						currentState = "hidden";
	    				break;
				}
			}
		
			public function openSelectedItem():void
			{
				if (selectedItem)
				{
					dispatchEvent(new Event("select"));	
					currentState="hidden";
				}
			}

			public function selectNext():void
			{
				selectedIndex++;
			}		
			
			public function selectPrevious():void
			{
				if (selectedIndex > 0) selectedIndex--;
			}		

			private function resultHandler(event:ResultEvent):void
			{
				dataProvider = event.result as ArrayCollection;
				if (dataProvider && dataProvider.length > 0)
				{
					selectedIndex = 0;
				}
			}
			
		]]>
	</mx:Script>

	<mx:RemoteObject id="srv" destination="product" result="resultHandler(event)"/>

	<mx:states>
		<mx:State name="hidden">
			<mx:SetProperty name="visible" value="false"/>
		</mx:State>
	</mx:states>
	
</mx:List>